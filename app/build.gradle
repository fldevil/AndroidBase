apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt' // butterKnife
apply plugin: 'org.greenrobot.greendao' // greenDao

android {

    buildToolsVersion "23.0.3"
    compileSdkVersion 25

    defaultConfig {
        targetSdkVersion 22 // 再高就动态权限
        minSdkVersion 14 // 包括4.0以上
        multiDexEnabled true // 解决jar包冲突
        applicationId "com.bjxrgz.startup" // 要和manifest一致
        versionCode 1
        versionName "1.0.0"
    }

    signingConfigs { // 签名信息
        bjxrgz_debug {
            keyAlias 'Bjxrgz'
            keyPassword 'Bjxrgz998'
//            storeFile file('/Users/mengfd/Documents/projects/kaola/launch/android/bjxrgz_android_debug.jks')
//            storeFile file('E:/android_key/bjxrgz_android_debug.jks')
            storeFile file('D:/gitRep/Android/key/bjxrgz_android_debug.jks')
            storePassword 'Bjxrgz998'
        }
    }

    buildTypes { // 打包选项
        debug {
            signingConfig signingConfigs.bjxrgz_debug // 设置签名信息
            minifyEnabled false // 混淆开关
            // 指定混淆规则文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

//        jnidebug { // debug的一个扩展
//            initWith debug // 复制debug的属性和签名配置
//            applicationIdSuffix ".jnidebug"
//            jniDebuggable true //开启Jni调试
//        }

        release {
            signingConfig signingConfigs.bjxrgz_debug // 可以只用一个签名信息
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors { // 发布渠道
    }

    sourceSets { // 源码设置
        main {
            jniLibs.srcDirs = ['libs'] // 指定lib库目录
        }
        instrumentTest.setRoot('tests') // 指定测试所在的路径
        debug.setRoot('build-types/debug') // 指定debug模式的路径
        release.setRoot('build-types/release') // 指定release模式的路径
    }

    compileOptions { // 编码格式等
        encoding "UTF-8"
//        sourceCompatibility JavaVersion.VERSION_1_8
//        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions { // 编译的lint检查
        disable 'MissingTranslation' // 防止在发布的时候出现因MissingTranslation导致Build Failed!
        abortOnError false // 移除lint检查的error
    }

    packagingOptions { // 解决jar包冲突
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INFTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions { // bug value-2
        javaMaxHeapSize "4g"
    }

    // 以下两句防止图片导入报错
    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false
}

greendao {
    schemaVersion 1
    daoPackage 'com.bjxrgz.startup.db'
    targetGenDir 'src/main/java'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs') // 编译本地 jar 包
    testCompile 'junit:junit:4.12' // 仅仅针对单元测试的代码编译打包
    compile project(':PushSDK') // 友盟推送
    compile 'com.umeng.analytics:analytics:latest.integration' // 友盟统计
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:support-v4:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0' // 注解
    compile 'com.jakewharton:butterknife:8.4.0' // 注解
    compile 'com.orhanobut:logger:1.3' // 打印
    compile 'com.google.code.gson:gson:2.4' // json解析
    compile 'com.github.bumptech.glide:glide:3.7.0' // 图片加载缓存
    compile 'com.squareup.retrofit2:retrofit:2.1.0' // retrofit2
    compile 'com.squareup.retrofit2:converter-gson:2.0.1' // retrofit2构造器
    compile 'com.squareup.retrofit2:converter-scalars:2.1.0' // retrofit2构造器
    compile 'io.reactivex:rxandroid:1.2.1' // rxAndroid
    compile 'io.reactivex:rxjava:1.1.6' // rxJava
    compile 'org.greenrobot:greendao:3.2.0' // greendao 数据库
    compile 'org.greenrobot:greendao-generator:3.2.0' // greendao 数据库
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:v2.0.0' // recyclerView适配器
    compile 'de.hdodenhof:circleimageview:2.0.0' // 圆形图片
    compile 'com.github.chrisbanes:PhotoView:1.3.1' // 缩放图片
    compile 'org.greenrobot:eventbus:3.0.0' // 事件总线
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.2.0' // 动态权限
    compile 'com.github.hotchemi:permissionsdispatcher:2.2.0' // 动态权限

}
